
group 'dk.bankdata.prometheus'

apply plugin: 'java'
apply plugin: 'signing'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'
apply plugin: 'checkstyle'

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

sourceCompatibility = 1.8
targetCompatibility = 1.8

checkstyle {
    toolVersion = '8.12'
}

test {
    jacoco {
        includes = ['*.*']
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile(group: 'io.prometheus', name: 'simpleclient_servlet', version: project['io.prometheus.version'])
    testCompile(group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: project['org.junit.jupiter.version'])
    testRuntime(group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: project['org.junit.jupiter.version'])
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
    tasks.publishMavenJavaPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenJavaPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives sourceJar
    archives javadocJar
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId 'dk.bankdata.prometheus'
            artifactId 'jaxrs-exporter'
            version version

            from components.java

            pom.withXml {
                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)
                def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
            }

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }

            project.tasks.signArchives.signatureFiles.each {
                artifact(it) {
                    def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
                    if (matcher.find()) {
                        classifier = matcher.group(1)
                    } else {
                        classifier = null
                    }
                    extension = 'jar.asc'
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username project.hasProperty('ossrhUsername') ? ossrhUsername : ''
                password project.hasProperty('ossrhPassword') ? ossrhPassword : ''
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            name 'Simple API Types'
            description 'Simple types for use in implementing financial APIs'
            url 'https://github.com/Bankdata/simple-types'
            scm {
                connection 'scm:git:https://github.com/Bankdata/simple-types.git'
                developerConnection 'scm:git:https://github.com/Bankdata/simple-types.git'
                url 'https://github.com/Bankdata/simple-types'
                tag 'HEAD'
            }
            licenses {
                license {
                    name 'The MIT License'
                    url 'https://opensource.org/licenses/MIT'
                }
            }
            developers {
                developer {
                    id 'lange'
                    name 'Thor Anker Kvisg√•rd Lange'
                    email 'tel@bankdata.dk'
                    organization 'Bankdata'
                    organizationUrl 'https://www.bankdata.dk'
                }
            }
        }
    }
}
